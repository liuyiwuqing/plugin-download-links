plugins {
    id 'java'
    id "io.freefair.lombok" version "8.14"
    id "run.halo.plugin.devtools" version "0.6.2"
}

group 'site.muyin.downloadlinks'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.21.0')
    compileOnly 'run.halo.app:api'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

tasks.register('processUiResources', Copy) {
    from project(':ui').layout.buildDirectory.dir('dist')
    into layout.buildDirectory.dir('resources/main/console')
    dependsOn project(':ui').tasks.named('assemble')
    shouldRunAfter tasks.named('processResources')
}

tasks.named('classes') {
    dependsOn tasks.named('processUiResources')
}

halo {
    externalUrl = 'https://plugin.orb.local'
    version = '2.22.0'
    debug = true
//    suspend = true
    port = 8686
    debugPort = 8787
    containerName = 'plugin'
}